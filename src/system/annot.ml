open Types.Base
open Types.Tvar
open Env

module Annot = struct
  type branch = BType of t | BSkip
  [@@deriving show]
  and inter = t list
  [@@deriving show]
  and part = (typ * t) list
  [@@deriving show]
  and a =
  | AConst | AAtom
  | AAbstract of typ
  | AAx of Subst.t
  | ALet of t * part
  | AApp of t * t | ACons of t * t
  | AProj of t | ATag of t | AConstr of t | ACoerce of typ * t
  | AUpdate of t * t option
  | ATuple of t list
  | AIte of t * branch * branch
  | ACf of t * branch * branch
  | ALambda of typ * t
  | ALambdaRec of (typ * t) list
  | AInter of inter
  [@@deriving show]
  and t = { mutable cache: typ option ; ann: a }
  [@@deriving show]

  let substitute s t =
    let rec aux t =
      let ann = match t.ann with
      | AConst -> AConst | AAtom -> AAtom
      | AAbstract t -> AAbstract (Subst.apply s t)
      | AAx s' -> AAx (Subst.compose_restr s s')
      | ALet (t, ps) -> ALet (aux t, List.map (fun (ty, t) -> Subst.apply s ty, aux t) ps)
      | AApp (t1, t2) -> AApp (aux t1, aux t2)
      | ACons (t1, t2) -> ACons (aux t1, aux t2)
      | AProj t -> AProj (aux t) | ATag t -> ATag (aux t)
      | AConstr t -> AConstr (aux t)
      | ACoerce (ty, t) -> ACoerce (Subst.apply s ty, aux t)
      | AUpdate (t, ot) -> AUpdate (aux t, Option.map aux ot)
      | ATuple ts -> ATuple (List.map aux ts)
      | AIte (t,b1,b2) -> AIte (aux t, aux_b b1, aux_b b2)
      | ACf (t,b1,b2) -> ACf (aux t, aux_b b1, aux_b b2)
      | ALambda (ty, t) -> ALambda (Subst.apply s ty, aux t)
      | ALambdaRec lst -> ALambdaRec (List.map (fun (ty,t) -> (Subst.apply s ty, aux t)) lst)
      | AInter ts -> AInter (List.map aux ts)
    in { cache=Option.map (Subst.apply s) t.cache ; ann }
    and aux_b b =
      match b with BSkip -> BSkip | BType t -> BType (aux t)  
    in
    aux t

  let nc a = { cache=None ; ann=a }
end

module IAnnot = struct
  type coverage = (Eid.t * typ) option * REnv.t
  [@@deriving show]
  type branch = BType of t | BSkip | BInfer
  [@@deriving show]
  and inter_branch = { coverage: coverage option ; ann: t }
  [@@deriving show]
  and inter = inter_branch list
  [@@deriving show]
  and part = (typ * t) list
  [@@deriving show]
  and t =
  | A of Annot.t
  | Infer
  | Untyp
  | ALet of t * part
  | AApp of t * t | ACons of t * t
  | AProj of t | ATag of t | AConstr of t | ACoerce of typ * t
  | AUpdate of t * t option
  | ATuple of t list
  | AIte of t * branch * branch
  | ACf of t * branch * branch
  | ALambda of typ * t
  | ALambdaRec of (typ * t) list
  | AInter of inter
  [@@deriving show]

  let substitute s =
    let rec aux t =
      match t with
      | A a -> A (Annot.substitute s a)
      | Infer -> Infer
      | Untyp -> Untyp
      | ALet (t, ps) -> ALet (aux t, List.map (fun (ty, t) -> Subst.apply s ty, aux t) ps)
      | AApp (t1, t2) -> AApp (aux t1, aux t2)
      | ACons (t1, t2) -> ACons (aux t1, aux t2)
      | AProj t -> AProj (aux t) | ATag t -> ATag (aux t)
      | AConstr t -> AConstr (aux t)
      | ACoerce (ty, t) -> ACoerce (Subst.apply s ty, aux t)
      | AUpdate (t, ot) -> AUpdate (aux t, Option.map aux ot)
      | ATuple ts -> ATuple (List.map aux ts)
      | AIte (t,b1,b2) -> AIte (aux t, aux_b b1, aux_b b2)
      | ACf (t,b1,b2) -> ACf (aux t, aux_b b1, aux_b b2)
      | ALambda (ty, t) -> ALambda (Subst.apply s ty, aux t)
      | ALambdaRec lst -> ALambdaRec (List.map (fun (ty,t) -> (Subst.apply s ty, aux t)) lst)
      | AInter bs -> AInter (List.map aux_ib bs)
    and aux_b b =
      match b with
      | BType t -> BType (aux t)
      | BInfer -> BInfer | BSkip -> BSkip
    and aux_ib { coverage ; ann } =
      let aux_coverage (o,renv) =
        let o = o |> Option.map (fun (eid,ty) -> (eid, Subst.apply s ty)) in
        let renv = REnv.substitute s renv in
        (o, renv)
      in
      let coverage = Option.map aux_coverage coverage in
      let ann = aux ann in
      { coverage ; ann }
    in
    aux
end